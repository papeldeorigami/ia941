pushd "./default" 
source selection.soar
popd

learn --on

sp {elaborate*top-state*top-state
   (state <s> ^superstate nil)
-->
   (<s> ^top-state <s>)
}

sp {elaborate*state*top-state
   (state <s> ^superstate.top-state <ts>)
-->
   (<s> ^top-state <ts>)
}

sp {elaborate*state*name
   (state <s> ^superstate.operator.name <name>)
-->
   (<s> ^name <name>)
}

sp {propose*init
   (state <s> ^superstate nil
             -^init)
-->
   (<s> ^operator <o> +)
   (<o> ^name init)
}

sp {apply*init
   (state <s> ^operator.name init)
-->
   (<s> ^name solution
        ^list <list> 
        ^init true 
        ^answer <answer>
        ^desired <d>)
   (<list> ^direction 1 ^direction 2 ^direction 3)
   (<d> ^answer.direction 3)
   (<answer> ^direction none)
   (write (crlf) | Init |)
}

sp {propose*move
   (state <s> ^superstate nil
              ^init true
              ^list <list>)
-->
   (<s> ^operator <o> +)
   (<o> ^name move
        ^parameter <p>)
   (<p> ^list-copy (deep-copy <list>))
}

sp {elaborate*move*impasse
   (state <s> ^impasse no-change
              ^superstate <ss>)
   (<ss> ^answer <answer>
         ^desired <desired>)
-->
   (<s> ^answer <answer>
        ^desired <desired>)
}

sp {apply*move
   (state <s> ^operator.name move
              ^target <target>)
-->
   (write (crlf) | Move: | <target> ||)
   (halt)
}

sp {propose*move*plan
   (state <s> ^impasse no-change
              ^superstate <ss>)
   (<ss> ^operator <o>)
   (<o> ^name move
        ^parameter <p>)
   (<p> ^list-copy <list>)
   (<list> ^direction <direction>)
-->
   (<s> ^operator <op> +)
   (<op> ^name move-plan
        ^parameter <param>)
   (<param> ^direction <direction>)
   (write (crlf) | Propose move to | <direction> ||)
}

#sp {propose*move*plan
#   (state <s> ^list <list>)
#   (<list> ^direction <direction>)
#-->
#   (<s> ^operator <op> +)
#   (<op> ^name move-plan
#         ^parameter <param>)
#   (<param> ^direction <direction>)
#   (write (crlf) | Propose move to | <direction> ||)
#}

sp {apply*move*plan
   (state <s> ^operator <o>
              ^answer <answer>)
   (<answer> ^direction <old-direction>)
   (<o> ^name move-plan
        ^parameter.direction <direction>)
-->
   (<answer> ^direction <old-direction> -
             ^direction <direction>)
#   (<s> ^numeric-value <direction>)
   (write (crlf) | Apply move plan to direction: | <direction> ||)
}

sp {elaborate*problem-space
   (state <s> ^name move)
-->
   (<s> ^problem-space <p>)
   (<p> ^name move-plan
        ^default-state-copy yes
        ^two-level-attributes answer)
}

sp {evaluate*state*success
   (state <s> ^desired <d>
              ^problem-space.name move-plan
              ^tried-tied-operator
              ^answer.direction <direction>
              ^top-state <ts>)
   (<d> ^answer.direction <direction>)
-->
   (<s> ^success <d>)
   (<ts> ^target <direction>)
   (write (crlf) | ========= Success! ========== |)
}

sp {evaluate*state*failure
   (state <s> ^desired <d>
              ^problem-space.name move-plan
              ^tried-tied-operator
              ^answer.direction <direction>)
  -(<d> ^answer.direction <direction>)
-->
   (<s> ^failure <d>)
   (write (crlf) | xxxxxxxxx  Failure! xxxxxxxxxxx |)
}

