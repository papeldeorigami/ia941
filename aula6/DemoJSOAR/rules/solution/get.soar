sp {propose*get
   (state <s> ^name solution
              ^desired <desired>
              ^knapsack <knapsack>
              ^memory <memory>
              ^collectedNames <collectedNames>
              ^distances <distances>
              ^position <position>)
   (<position> ^X <creaturePositionX>)
   (<position> ^Y <creaturePositionY>)
   # find a jewel
   (<memory> ^ENTITY <entity>)
   (<entity> ^X <entityPositionX>)
   (<entity> ^Y <entityPositionY>)
   (<entity> ^NAME <entityName>)
   (<entity> ^TYPE JEWEL)
   (<entity> ^COLOR <color>)
   # move only if distance is less than 30
   (<distances> ^<entity> { <DISTANCE> < 30 })
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
   # do not get entities again
  -(<collectedNames> ^NAME <entityName>)
-->
   (<s> ^operator <o> +)
   (<o> ^name get)
   (<o> ^parameter <p>)
   (<p> ^NAME <entityName>)
#   (write (crlf) | propose get: name = | <entityName> | distance = | <DISTANCE> ||)
}

sp {solution*apply*get
   (state <s> ^operator <o>
#              ^io.input-link.CREATURE <creature>
              ^collectedNames <collectedNames>
              ^collected <collected>
              ^knapsack <knapsack>
              ^memory <memory>
              ^desired <desired>)
   (<o> ^name get
        ^parameter <p>)
   (<p> ^NAME <entityName>)
#   (<creature> ^SENSOR.VISUAL <visual>)
#   (<visual> ^ENTITY <visualEntity>)
#   (<visualEntity> ^NAME <entityName>)
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity> ^NAME <entityName>)
   (<memoryEntity> ^COLOR <color>)
   (<knapsack> ^<color> <quantity>
               ^Red <kred>
               ^Green <kgreen>
               ^Blue <kblue>
               ^Yellow <kyellow>
               ^Magenta <kmagenta>
               ^White <kwhite>)
   (<desired> ^<color> <dquantity>
              ^Red <dred>
              ^Green <dgreen>
              ^Blue <dblue>
              ^Yellow <dyellow>
              ^Magenta <dmagenta>
              ^White <dwhite>)
-->
   (<memory> ^ENTITY <memoryEntity> -)
   #(<visual> ^ENTITY <visualEntity> -)
   (<s> ^collected <collected> - (+ <collected> 1))
   (<collectedNames> ^NAME <entityName>)
   (<knapsack> ^<color> <quantity> - (+ <quantity> 1))
   (write (crlf) | get: name = | <entityName> | Knapsack was: |)
   (write (crlf) | Red=| <kred> |/| <dred> ||)
   (write (crlf) | Green=| <kgreen> |/| <dgreen> ||)
   (write (crlf) | Blue=| <kblue> |/| <dblue> ||)
   (write (crlf) | Yellow=| <kyellow> |/| <dyellow> ||)
   (write (crlf) | Magenta=| <kmagenta> |/| <dmagenta> ||)
   (write (crlf) | White=| <kwhite> |/| <dwhite> ||)
   (write (crlf) | ---> | <color> | is now | (+ <quantity> 1) |/| <dquantity> ||)
}

sp {solution*apply*actual*get*only*once
   (state <s> ^operator <o>
              ^io.input-link.CREATURE <creature>
              ^actions <actions>
              ^collected <collected>
              ^knapsack <knapsack>
              ^memory <memory>
              ^desired <desired>)
  -(<actions> ^<attr> <any>)  # hold the first action only
   (<o> ^name get
        ^parameter <p>)
   (<p> ^NAME <entityName>)
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity> ^NAME <entityName>)
-->
   (<actions> ^GET <command>)
   (<command> ^Name <entityName>)
   (write (crlf))
   (write (crlf) | ***** prepare output get for entity= | <entityName> ||)
   (write (crlf))
}

sp {solution*apply*get*evaluate*readyToDeliver
   (state <s> ^operator.name get
              ^collected 8    # actually 9 is the full knapsack, but we just got the last one, so it is 9 - 1 = 8
              ^readyToDeliver false)
-->
   (<s> ^readyToDeliver false -
        ^readyToDeliver true)
   (write (crlf) | **** Ready to deliver!|)
}

