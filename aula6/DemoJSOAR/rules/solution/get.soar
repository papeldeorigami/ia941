sp {propose*get
   (state <s> ^actions <actions>
              ^desired <desired>
              ^thing-ahead <entity>
             -^delivered
              ^io.input-link.CREATURE <creature>)
   (<creature> ^MEMORY <memory>
               ^KNAPSACK <knapsack>)
   (<entity> ^NAME <entityName>
             ^TYPE JEWEL
             ^COLOR <color>)
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
   # must be already in memory to ensure consistency
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity> ^NAME <entityName>)
   # if not getting something already
  -(<actions> ^GET)
-->
   (<s> ^operator <o> + =)
   (<o> ^name get
        ^parameter.NAME <entityName>)
   (write (crlf) | Propose get: name = | <entityName> ||)
}

sp {solution*apply*get
   (state <s> ^operator <o>
              ^actions <actions>)
   (<o> ^name get
        ^parameter <p>)
   (<p> ^NAME <entityName>)
-->
   (<actions> ^GET.Name <entityName>)
   (write (crlf) | Get: name=| <entityName> ||)
}
