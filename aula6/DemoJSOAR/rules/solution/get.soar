sp {propose*get
   (state <s> ^name solution
              ^desired <desired>
              ^knapsack <knapsack>
              ^memory <memory>
              ^io.input-link.CREATURE <creature>)
   (<creature> ^SENSOR.VISUAL.ENTITY <entity>
               ^POSITION <position>)
   # find a jewel
   (<entity> ^NAME <entityName>
             ^TYPE JEWEL
             ^COLOR <color>
             # get only if distance is less than 30
             ^DISTANCE { <DISTANCE> < 30 })
   # get only if already seen
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity> ^NAME <entityName>)
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
-->
   (<s> ^operator <o> + =)
   (<o> ^name get
        ^parameter <p>)
   (<p> ^NAME <entityName>)
   (write (crlf) | propose get: name = | <entityName> | distance = | <DISTANCE> ||)
}

sp {solution*apply*get
   (state <s> ^operator <o>
              ^knapsack <knapsack>
              ^memory <memory>
              ^desired <desired>
              ^actions <actions>
              ^io.input-link.CREATURE <creature>)
   # read the jewel name from parameters
   (<o> ^name get
        ^parameter <p>)
   (<p> ^NAME <entityName>)
   # lookup the entity in the visual sensor
   (<creature> ^SENSOR.VISUAL <visual>)
   (<visual> ^ENTITY <visualEntity>)
   (<visualEntity> ^NAME <entityName>)
   # lookup the entity in the memory
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity>
             ^NAME <entityName>
             ^COLOR <color>)
   # lookup the color status in the knapsack and in the desired state
   (<knapsack> ^<color> <quantity>
               ^Red <kred>
               ^Green <kgreen>
               ^Blue <kblue>
               ^Yellow <kyellow>
               ^Magenta <kmagenta>
               ^White <kwhite>)
   (<desired> ^<color> <dquantity>
              ^Red <dred>
              ^Green <dgreen>
              ^Blue <dblue>
              ^Yellow <dyellow>
              ^Magenta <dmagenta>
              ^White <dwhite>)
-->
   # remove element from the input (temporary)
   (<visual> ^ENTITY <visualEntity> -)
   # remove element from memory
   (<memory> ^ENTITY <memoryEntity> -)
   # increment the number of jewels of the entity's color in the knapsack
   (<knapsack> ^<color> <quantity> -
               ^<color> (+ <quantity> 1))
   # prepare the output action
   (<actions> ^GET <command>)
   (<command> ^Name <entityName>)
   (write (crlf) | get: name = | <entityName> | Knapsack was: |)
   (write (crlf) | Red=| <kred> |/| <dred> ||)
   (write (crlf) | Green=| <kgreen> |/| <dgreen> ||)
   (write (crlf) | Blue=| <kblue> |/| <dblue> ||)
   (write (crlf) | Yellow=| <kyellow> |/| <dyellow> ||)
   (write (crlf) | Magenta=| <kmagenta> |/| <dmagenta> ||)
   (write (crlf) | White=| <kwhite> |/| <dwhite> ||)
   (write (crlf) | ---> | <color> | is now | (+ <quantity> 1) |/| <dquantity> ||)
}

#sp {solution*apply*get*test
#   (state <s> ^operator <o>)
#   # read the jewel name from parameters
#   (<o> ^name get
#        ^parameter <p>)
#   (<p> ^NAME <entityName>)   
#-->
#   (write (crlf) | ======= apply get entityName=| <entityName> ||)
#   (write (crlf) (cmd print --depth 1 <s>))
#}
#
