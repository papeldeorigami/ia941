sp {propose*get
   (state <s> ^name solution
              ^desired <desired>
              ^memory <memory>
              ^knapsack <knapsack>
              ^position <position>)
   (<position> ^X <creaturePositionX>)
   (<position> ^Y <creaturePositionY>)
   # find a jewel
   (<memory> ^ENTITY <entity>)
   (<entity> ^X <entityPositionX>)
   (<entity> ^Y <entityPositionY>)
   (<entity> ^NAME <entityName>)
   (<entity> ^TYPE JEWEL)
   (<entity> ^COLOR <color>)
   # move only if distance is less than 30
   (<entity> ^distance { <distance> < 30 })
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
-->
   (<s> ^operator <o> +)
   (<o> ^name get)
   (<o> ^parameter <p>)
   (<p> ^NAME <entityName>)
   (write (crlf) | propose get: name = | <entityName> | distance = | <distance> ||)
}

sp {solution*apply*get
   (state <s> ^operator <o>
              ^io.input-link.CREATURE <creature>
              ^knapsack <knapsack>
              ^memory <memory>)
   (<o> ^name get)
   (<o> ^parameter <p>)
   (<p> ^NAME <name>)
   (<creature> ^SENSOR.VISUAL <visual>)
   (<visual> ^ENTITY <visualEntity>)
   (<visualEntity> ^NAME <name>)
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity> ^NAME <name>)
   (<memoryEntity> ^distance <distance>)
   (<memoryEntity> ^COLOR <color>)
   (<knapsack> ^<color> <quantity>)
-->
   (<memory> ^ENTITY <memoryEntity> -)
   (<visual> ^ENTITY <visualEntity> -)
   (<knapsack> ^<color> <quantity> - (+ <quantity> 1))
   (write (crlf) | apply get: name = | <name> | distance = | <distance> ||)
}
