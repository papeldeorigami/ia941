sp {propose*reset-plan
   (state <s>  ^planning true
               ^sent-command true)
-->
   (<s> ^operator <o> +)
   (<o> ^name reset-plan)
   (write (crlf) | Propose reset plan |)
}

sp {apply*reset-plan
   (state <s> ^operator.name reset-plan
              ^planning true
              ^io.input-link.CREATURE <creature>
              ^v-position <v-position>
              ^v-knapsack <v-knapsack>
              ^v-traveled-distance <v-traveled-distance>)
   (<creature> ^KNAPSACK <knapsack>
               ^POSITION <position>)
   (<v-position> ^X <vx>
                 ^Y <vy>)
   (<position> ^X <px>
               ^Y <py>)
   (<knapsack> ^Red <kred>
               ^Green <kgreen>
               ^Blue <kblue>
               ^Yellow <kyellow>
               ^Magenta <kmagenta>
               ^White <kwhite>)
   (<v-knapsack> ^Red <vred>
                 ^Green <vgreen>
                 ^Blue <vblue>
                 ^Yellow <vyellow>
                 ^Magenta <vmagenta>
                 ^White <vwhite>)
-->
   (<s> ^planning true -
        ^v-traveled-distance <v-traveled-distance> -
        ^v-traveled-distance 0)
   (<v-position> ^X <vx> -
                 ^Y <vy> -
                 ^X <px>
                 ^Y <py>)
   (<v-knapsack> ^Red <vred> -
                 ^Green <vgreen> -
                 ^Blue <vblue> -
                 ^Yellow <vyellow> -
                 ^Magenta <vmagenta> -
                 ^White <vwhite> -
                 ^Red <kred>
                 ^Green <kgreen>
                 ^Blue <kblue>
                 ^Yellow <kyellow>
                 ^Magenta <kmagenta>
                 ^White <kwhite>)
   (write (crlf) | ---- Reset planning variables |)
}

sp {apply*reset-plan*collected-names
   (state <s> ^operator.name reset-plan
              ^v-collected-names <v-collected-names>)
   (<v-collected-names> ^<entityName> <value>)
-->
   (<v-collected-names> ^<entityName> <value> -)
   (write (crlf) | ---- Removed | <entityName> | from planned collected names |)
}

