sp {propose*v-get
   (state <s> ^name solution
              ^desired <desired>
              ^memory <memory>
              ^v-knapsack <knapsack>
              ^v-collected-names <collectedNames>
              ^v-distances <distances>)
   # find a jewel
   (<memory> ^ENTITY <entity>)
   (<entity> ^X <entityPositionX>
             ^Y <entityPositionY>
             ^NAME <entityName>
             ^TYPE JEWEL
             ^COLOR <color>)
   # get only if distance is less than 30
   (<distances> ^<entityName> { <DISTANCE> < 30 })
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <dquantity>)
   (<knapsack> ^<color> { <quantity> < <dquantity> })
   # do not get doubled entities
  -(<collectedNames> ^NAME <entityName>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name v-get)
   (<o> ^parameter <p>)
   (<p> ^NAME <entityName>
        ^COLOR <color>)
   (write (crlf) | Propose v-get: name = | <entityName> | distance = | <DISTANCE> ||)
   (write (crlf) | collectedNames=| <collectedNames> | distances=| <distances> | entity=| <entity> | entityPositionX=| <entityPositionX> | entityPositionY=| <entityPositionY> | color=| <color> | dquantity=| <dquantity> | quantity=| <quantity> ||)
   (write (crlf) (cmd print --depth 2 <memory>))
}

sp {solution*apply*v-get
   (state <s> ^operator <o>
              ^v-collected-names <collectedNames>
              ^v-knapsack <knapsack>
              ^desired <desired>)
   # read the jewel name from parameters
   (<o> ^name v-get
        ^parameter <p>)
   (<p> ^NAME <entityName>
        ^COLOR <color>)
   # lookup the color status in the knapsack and in the desired state
   (<knapsack> ^<color> { <quantity> < <dquantity> }
               ^Red <kred>
               ^Green <kgreen>
               ^Blue <kblue>
               ^Yellow <kyellow>
               ^Magenta <kmagenta>
               ^White <kwhite>)
   (<desired> ^<color> <dquantity>
              ^Red <dred>
              ^Green <dgreen>
              ^Blue <dblue>
              ^Yellow <dyellow>
              ^Magenta <dmagenta>
              ^White <dwhite>)
-->
   (<collectedNames> ^NAME <entityName>)
   (<knapsack> ^<color> <quantity> - (+ <quantity> 1))
   (write (crlf) | Apply v-get: name = | <entityName> | Knapsack was: |)
   (write (crlf) | Red=| <kred> |/| <dred> ||)
   (write (crlf) | Green=| <kgreen> |/| <dgreen> ||)
   (write (crlf) | Blue=| <kblue> |/| <dblue> ||)
   (write (crlf) | Yellow=| <kyellow> |/| <dyellow> ||)
   (write (crlf) | Magenta=| <kmagenta> |/| <dmagenta> ||)
   (write (crlf) | White=| <kwhite> |/| <dwhite> ||)
   (write (crlf) | ---> | <color> | is now | (+ <quantity> 1) |/| <dquantity> ||)
}
