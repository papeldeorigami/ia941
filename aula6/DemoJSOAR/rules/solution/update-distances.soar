sp {update-distances*propose*update-distance
   (state <s> ^name update-distances
              ^memory <memory>)
   # find a jewel
   (<memory> ^ENTITY <entity>)
   (<entity> ^NAME <entityName>)
   -(<entity> ^distance <distance>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name update-distance)
   (<op> ^parameter <newEntity>)
   (<newEntity> ^NAME <entityName>)
   (write (crlf) | propose update distance: name = | <entityName> ||)
}

sp {update-distances*apply*update-distance
   (state <s> ^operator <o>
              ^memory <memory>
              ^position <position>)
   (<position> ^X <px>)
   (<position> ^Y <py>)
   (<o> ^name update-distance)
   (<o> ^parameter.NAME <entityName>)
   (<memory> ^ENTITY <entity>)
   (<entity> ^NAME <entityName>)
   (<entity> ^X <x>)
   (<entity> ^Y <y>)
-->
   (<entity> ^distance (sqrt (+ (* (- <px> <x>) (- <px> <x>)) (* (- <py> <y>) (- <py> <y>)))))
   (write (crlf) | apply update distance: name = | <entityName> | x = | <x> | y = | <y> ||)
}