sp {propose*move*jewel
   (state <s> ^name solution
              ^memory.ENTITY <entity>
              ^desired <desired>
              ^v-distances <distances>
              ^v-knapsack <knapsack>
              ^v-collected-names <collectedNames>)
   (<entity> ^X <entityPositionX>
             ^Y <entityPositionY>
             ^NAME <entityName>
             ^TYPE JEWEL
             ^COLOR <color>)
   # move only if distance is high
   (<distances> ^<entityName> { <DISTANCE> > 30 })
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
   # do not get doubled entities
  -(<collectedNames> ^<entityName>)
-->
   (<s> ^operator <o> +)
   (<o> ^name move)
   (<o> ^parameter <p>)
   (<p> ^ENTITY <entity>
        ^NAME <entityName>
        ^X <entityPositionX>
        ^Y <entityPositionY>
        ^COLOR <color>
        ^DISTANCE <DISTANCE>)
   (write (crlf) | propose move (to jewel): name=| <entityName> | color=| <color> | distance=| <DISTANCE> ||)
}  

sp {apply*move
   (state <s> 
        ^operator <o>
        ^desired <desired>
        ^v-position <position>
        ^v-distances <distances>
        ^v-knapsack <knapsack>
        ^v-collected-names <collectedNames>
        ^v-traveled-distance <traveledDistance>)
   (<position> ^X <oldX>
               ^Y <oldY>)
   (<o> ^name move
        ^parameter <p>)
   (<p> ^ENTITY <entity>
        ^NAME <entityName>
        ^X <x>
        ^Y <y>
        ^COLOR <color>
        ^DISTANCE <DISTANCE>)
   (<knapsack> ^<color> <quantity>)
   (<desired> ^<color> <dquantity>)
-->
   (<collectedNames> ^<entityName> true)
   (<position> ^X <oldX> -
               ^Y <oldY> -
               ^X <x>
               ^Y <y>)
   (<knapsack> ^<color> <quantity> - (+ <quantity> 1))
   (<s> ^v-traveled-distance <traveledDistance> -
        ^v-traveled-distance (+ <traveledDistance> <DISTANCE>))
   (write (crlf) | Test move: name=| <entityName> | color=| <color> | distance=| <DISTANCE> ||)
   (write (crlf) | Virtual get | <color> | | (+ <quantity> 1) |/| <dquantity> ||)
}

sp {apply*move*update*v-distance
   (state <s>
        ^operator <o>
        ^memory.ENTITY <entity>
        ^v-distances <v-distances>
        ^v-position <v-position>)
   (<o> ^name move
        ^parameter <p>)
   (<p> ^NAME <entityName>)
   (<v-position> ^X <px>
                 ^Y <py>)
   (<entity> ^NAME { <otherName> <> <entityName> }
             ^X <x>
             ^Y <y>)
   (<v-distances> ^<otherName> <oldDistance>)
-->
   (<v-distances> ^<otherName> <oldDistance> -
                  ^<otherName> (sqrt (+ (* (- <px> <x>) (- <px> <x>)) (* (- <py> <y>) (- <py> <y>)))))
   (write (crlf) | apply*move*update*v-distance for | <otherName> ||)
#   (write (crlf) (cmd print --depth 2 <v-distances>))
}

sp {apply*actual*move*only*once
   (state <s> 
        ^operator <o>
        ^actions <actions>)
  -(<actions> ^<attr> <any>)  # hold the first action only
   (<o> ^name move
        ^parameter <p>)
   (<p> ^ENTITY <entity>
        ^NAME <entityName>
        ^X <x>
        ^Y <y>)
-->
   (<actions> ^MOVE <command>)
   (<command> ^Vel 1)
   (<command> ^VelR 1)
   (<command> ^VelL 1)
   (<command> ^X <x>)
   (<command> ^Y <y>)
   (write (crlf))
   (write (crlf) | ***** prepare action move to entity=| <entityName> | x=| <x> | y=| <y> ||)
   (write (crlf))
   #(write (crlf) | apply move: name = | <entityName> | distance = | <DISTANCE> ||)
}

sp {solution*apply*move*set-planning
   (state <s> ^operator.name move
              ^actions
             -^planning)
-->
   (<s> ^planning true)
#   (write (crlf) | Set planning mode to true |)
}
