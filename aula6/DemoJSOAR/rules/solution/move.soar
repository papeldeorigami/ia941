sp {solution*propose*move*jewel
   (state <s> ^name solution
              ^memory.ENTITY <entity>
              ^desired <desired>
              ^v-position <position>
              ^v-distances <distances>
              ^v-knapsack <knapsack>
              ^v-traveled-distance <traveledDistance>)
   (<position> ^X <x>
               ^Y <y>)
   (<entity> ^X <entityPositionX>
             ^Y <entityPositionY>
             ^NAME <entityName>
             ^TYPE JEWEL
             ^COLOR <color>)
   (<distances> ^<entityName> { <DISTANCE> > 30 }) # move only if distance is greater than 30
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
-->
   (<s> ^operator <o> +)
   (<o> ^name move)
   (<o> ^parameter <p>)
   (<p> ^ENTITY <entity>
        ^NAME <entityName>
        ^X <entityPositionX>
        ^Y <entityPositionY>
        ^DISTANCE <DISTANCE>)
#   (write (crlf) | propose move (to jewel): name = | <entityName> | distance = | <DISTANCE> ||)
}  

sp {solution*apply*move
   (state <s> 
        ^operator <o>
        ^v-position <position>
        ^v-distances <distances>
        ^v-traveled-distance <traveledDistance>)
   (<position> ^X <oldX>
               ^Y <oldY>)
   (<o> ^name move
        ^parameter <p>)
   (<p> ^ENTITY <entity>
        ^NAME <entityName>
        ^DISTANCE <DISTANCE>
        ^X <x>
        ^Y <y>)
-->
   (<position> ^X <oldX> -
               ^Y <oldY> -
               ^X <x>
               ^Y <y>)
   (<distances> ^<entityName> <DISTANCE> - ^<entityName> 0)
   (<s> ^v-traveled-distance <traveledDistance> -
        ^v-traveled-distance (+ <traveledDistance> <DISTANCE>))
   (write (crlf) | try move: name = | <entityName> | traveledDistance = | <traveledDistance> | + | <DISTANCE> ||)
}

sp {solution*apply*actual*move*only*once
   (state <s> 
        ^operator <o>
        ^actions <actions>)
  -(<actions> ^<attr> <any>)  # hold the first action only
   (<o> ^name move
        ^parameter <p>)
   (<p> ^ENTITY <entity>
        ^NAME <entityName>
        ^X <x>
        ^Y <y>)
-->
   (<actions> ^MOVE <command>)
   (<command> ^Vel 1)
   (<command> ^VelR 1)
   (<command> ^VelL 1)
   (<command> ^X <x>)
   (<command> ^Y <y>)
   (write (crlf))
   (write (crlf) | ***** prepare output move to entity = | <entityName> | x = | <x> | y = | <y> ||)
   (write (crlf))
   #(write (crlf) | apply move: name = | <entityName> | distance = | <DISTANCE> ||)
}
