sp {solution*propose*move
   (state <s> ^name solution
              ^position <position>
              ^memory <memory>
              ^knapsack <knapsack>
              ^desired <desired>)
   (<position> ^X <creaturePositionX>)
   (<position> ^Y <creaturePositionY>)
   # find a jewel
   (<memory> ^ENTITY <entity>)
   (<entity> ^X <entityPositionX>)
   (<entity> ^Y <entityPositionY>)
   (<entity> ^NAME <entityName>)
   (<entity> ^TYPE JEWEL)
   (<entity> ^COLOR <color>)
   # move only if distance is greater than 30
   (<entity> ^DISTANCE { <DISTANCE> > 30 })
   # filter colors only that are missing in some leaflet
   (<desired> ^<color> <desiredQuantity>)
   (<knapsack> ^<color> { <quantity> < <desiredQuantity> })
-->
   (<s> ^operator <o> +)
   (<o> ^name move)
   (<o> ^parameter <p>)
   (<p> ^NAME <entityName>)
   (<p> ^X <entityPositionX>)
   (<p> ^Y <entityPositionY>)
   (<p> ^DISTANCE <DISTANCE>)
   (write (crlf) | propose move: name = | <entityName> | distance = | <DISTANCE> ||)
}
   
sp {solution*apply*move
   (state <s> 
        ^operator <o>
        ^position <position>
        ^traveledDistance <traveledDistance>)
   (<position> ^X <oldX>)
   (<position> ^Y <oldY>)
   (<o> ^name move)
   (<o> ^parameter <p>)
   (<p> ^NAME <entityName>)
   (<p> ^DISTANCE <DISTANCE>)
   (<p> ^X <x>)
   (<p> ^Y <y>)
-->
   (<position> ^X <oldX> -)
   (<position> ^Y <oldY> -)
   (<position> ^X <x>)
   (<position> ^Y <y>)
   #   (<s> ^actions <actions>)
   #   (<actions> ^MOVE <command>)
   #   (<command> ^Vel 1)
   #   (<command> ^VelR 1)
   #   (<command> ^VelL 1)
   #   (<command> ^X <x>)
   #   (<command> ^Y <y>)
   (<s> ^traveledDistance <traveledDistance> -
        ^traveledDistance (+ <traveledDistance> <DISTANCE>))
   (write (crlf) | apply move: name = | <entityName> | traveledDistance = | <traveledDistance> | + | <DISTANCE> ||)
   #(write (crlf) | apply move: name = | <entityName> | distance = | <DISTANCE> ||)
}

sp {solution*apply*move*update-distances
   (state <s> 
        ^operator <o>
        ^position <position>
        ^memory <memory>)
   (<o> ^name move)
   (<position> ^X <newX>)
   (<position> ^Y <newY>)
   (<memory> ^ENTITY <entity>)
   (<entity> ^NAME <entityName>)
   (<entity> ^X <x>)
   (<entity> ^Y <y>)
   (<entity> ^creatureX {<oldX> <> <newX>})
   (<entity> ^creatureY {<oldY> <> <newY>})
   (<entity> ^DISTANCE <DISTANCE>)
-->
   (<entity> ^DISTANCE <DISTANCE> -
             ^DISTANCE (sqrt (+ (* (- <newX> <x>) (- <newX> <x>)) (* (- <newY> <y>) (- <newY> <y>)))))
   (<entity> ^creatureX <oldX> - ^creatureX <newX>)
   (<entity> ^creatureY <oldY> - ^creatureY <newY>)
   (write (crlf) | apply move*update-distances: name = | <entityName> | oldX = | <oldX> | oldY = | <oldY> | newX = | <newX> | newY = | <newY> ||)
}

