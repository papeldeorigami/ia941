# See Entity means: save entity (Jewel or Food) to the state's attribute called memory
sp {solution*propose*see-entity
   (state <s> ^name start
              ^memory <memory>)
   (<s> ^io.input-link.CREATURE <creature>)
   (<creature> ^SENSOR.VISUAL.ENTITY <entity>)
   (<entity> ^TYPE <type> << JEWEL FOOD >>)
   (<entity> ^COLOR <color>)
   (<entity> ^X <x>)
   (<entity> ^Y <y>)
   (<entity> ^NAME <name>)
  -(<memory> ^ENTITY.NAME <name>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name see-entity)
   (<op> ^parameterEntity <newEntity>)
   (<newEntity> ^Name <name>)
   (<newEntity> ^Type <type>)
   (<newEntity> ^X <x>)
   (<newEntity> ^Y <y>)
   (<newEntity> ^Color <color>)
}

sp {solution*apply*see-entity
   (state <s> ^operator <op>
              ^memory <memory>)
   (<op> ^name see-entity)
   (<op> ^parameterEntity <newEntity>)
   (<newEntity> ^X <x>)
   (<newEntity> ^Y <y>)
   (<newEntity> ^Name <name>)
   (<newEntity> ^Type <type>)
   (<newEntity> ^Color <color>)
-->
   (<memory> ^ENTITY <memoryEntity>)
   (<memoryEntity> ^X <x>)
   (<memoryEntity> ^Y <y>)
   (<memoryEntity> ^NAME <name>)
   (<memoryEntity> ^TYPE <type>)
   (<memoryEntity> ^COLOR <color>) 
}