sp {init*elaborate*name-content-value
   (state <s> ^type state)
-->
   (<s> ^name-content-value <c1> <c2> <c3> <c4>
    <c5> <c6> <c7> <c8>)
   (<c1> ^name move ^content empty ^value 0)
   (<c2> ^name move ^content eater ^value 0)
   (<c3> ^name move ^content normalfood ^value 5)
   (<c4> ^name move ^content bonusfood ^value 10)
   (<c5> ^name jump ^content empty ^value -5)
   (<c6> ^name jump ^content eater ^value -5)
   (<c7> ^name jump ^content normalfood ^value 0)
   (<c8> ^name jump ^content bonusfood ^value 5)
}

sp {elaborate*operator*value
   (state <s> ^operator <o> +
              ^name-content-value <ccv>)
   (<o> ^name <name> ^content <content>)
   (<ccv> ^name <name> ^content <content> ^value <value>)
-->
   (<o> ^value <value>)}

sp {select*compare*best*value
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^value <v>)
   (<o2> ^value < <v>)
-->
   (<s> ^operator <o1> > <o2>)}

sp {select*move*bonusfood-better-than-normalfood-empty-eater
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name { << jump move >> <name> }
         ^content bonusfood)
   (<o2> ^name <name>
         ^content << normalfood empty eater >>)
-->
   (<s> ^operator <o1> > <o2>)
}
                  
sp {select*jump*bonusfood-better-than*move*empty
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name jump
         ^content bonusfood)
   (<o2> ^name move
         ^content empty)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {select*move*prefer-normalfood
   (state <s> ^operator <o> +)
   (<o> ^name move
        ^content normalfood)
-->
   (<s> ^operator <o> >)}

sp {select*move*bonusfood-better-than-normalfood-empty-eater
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name move
         ^content bonusfood)
   (<o2> ^name move
         ^content << normalfood empty eater >>)
-->
   (<s> ^operator <o1> > <o2>)}

sp {select*move*avoid-empty-eater
   (state <s> ^operator <o> +)
   (<o> ^name move ^content << empty eater >>)
-->
   (<s> ^operator <o> <)}